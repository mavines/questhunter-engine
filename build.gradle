buildscript {
    ext.kotlin_version = '1.3.71'
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}


apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 29
        versionCode 1
        versionName "0.1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

group = 'com.github.mavines'
def libGroupId = 'hbg.qh'
def libArtifactId = 'engine'
def libVersion = '0.1.1'
def libDesc = 'This is the library for creating custom adventures for Quest Hunter'
def libLicenses = ['MIT']

project.afterEvaluate {
    task sourceJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        archiveClassifier.set("sources")
    }

    publishing {
        publications {
            engine(MavenPublication) {
                groupId libGroupId
                artifactId libArtifactId
                version libVersion

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    // Add KotlinStdlib
                    def kotlinStdlibDepNode = dependenciesNode.appendNode('dependency')
                    kotlinStdlibDepNode.appendNode('groupId', kotlinStdlibGroupId)
                    kotlinStdlibDepNode.appendNode('artifactId', kotlinStdlibArtifactId)
                    kotlinStdlibDepNode.appendNode('version', kotlinStdlibVersion)
                }

                artifact(sourceJar)
                artifact bundleReleaseAar
            }
        }
    }
}

repositories {
    google()
    jcenter()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
